plugins {
	id "java"
	id "application"
	id "idea"
	id "org.springframework.boot" version "2.6.3"
	id "io.spring.dependency-management" version "1.0.11.RELEASE"
	id "maven-publish"
	id "com.google.cloud.artifactregistry.gradle-plugin" version "2.1.5"
	id "com.google.cloud.tools.jib" version "3.2.0"
	id "com.moowork.node" version "1.3.1"
	id "com.heroku.sdk.heroku-gradle" version "2.0.0"
}

group = "by.vk.betting.api"
version = "1.0.0-rc.1"

application {
	mainClass = "by.vk.betting.api.ServiceApplication"
	applicationName = "api"
}

bootJar {
	enabled = true
	archiveFileName = "application.jar"
	manifest {
		attributes 'provider': 'gradle'
	}
}

springBoot {
	buildInfo()
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

test {
	useJUnitPlatform()
}

node {
	version = "16.9.0"
	distBaseUrl = "https://nodejs.org/dist"
	download = false
	npmWorkDir = file("build/nodejs/")
	nodeModulesDir = file("frontend/")
}

task bundle(type: NpmTask, dependsOn: npmInstall) {
	args = ['run', 'build']
}

processResources.dependsOn bundle

heroku {
	appName = "analytic-service"
	jdkVersion = 17
	includes = ["build/libs/application.jar"]
	processTypes(
			web: "java -jar build/libs/application.jar"
	)
}

repositories {
	mavenCentral()
	maven {
		url "artifactregistry://europe-west1-maven.pkg.dev/veekay-demo/analytic-java-repository"
	}
}

ext {
	set("logbackVersion", "0.1.5")
}

dependencies {
	//region spring
	implementation("org.springframework.boot:spring-boot-starter-web")
	//endregion

	//region logback
	implementation("ch.qos.logback.contrib:logback-json-classic:${logbackVersion}")
	implementation("ch.qos.logback.contrib:logback-jackson:${logbackVersion}")
	//endregion

	//region test
	testImplementation("org.springframework.boot:spring-boot-starter-test")
	//endregion
}

jib {
	from {
		image = "gcr.io/distroless/java17-debian11"
	}
	to {
		image = "fragaly/analytic-service"
		tags = ["$version", "latest"]
	}
	container {
		jvmFlags = ["-Xss256k",
					"-Xmx512m",
					"-Xms256m",
					"-XX:MaxMetaspaceSize=128m",
					"-XX:+AlwaysActAsServerClassMachine",
					"-XX:+ExitOnOutOfMemoryError",
					"-XX:+UseContainerSupport",
					"-XX:+UseStringDeduplication",
					"-XX:+ExitOnOutOfMemoryError",
					"-XX:+OptimizeStringConcat",
					"-XX:MaxRAMPercentage=75",
					"-XX:InitialRAMPercentage=50",
					"-XX:HeapDumpPath=/opt/tmp/heapdump.bin"]
		mainClass = "by.vk.betting.api.ServiceApplication"
		ports = ['80']
		labels = ["app-name": "analytic-service", "service-version": "$version"]
		creationTime = "USE_CURRENT_TIMESTAMP"
	}
}
